plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
}
group 'fr.techad'
version '2.1.0'
sourceCompatibility = 1.8
ext.withDependencies=project.hasProperty('withDependencies')
//Used to be able to add dependencies to jar
if(withDependencies){
configurations.implementation.setCanBeResolved(true)
configurations.api.setCanBeResolved(true)
}

java {
    withJavadocJar()
    withSourcesJar()
}
repositories {
    mavenCentral()
    mavenLocal()
}
dependencies {
    // light-4j
    implementation 'com.networknt:server:2.1.1'
    implementation 'ch.qos.logback:logback-classic:1.4.4'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.lucene:lucene-analysis-common:9.4.2'
    implementation 'org.apache.lucene:lucene-core:9.4.0'
    implementation 'org.apache.lucene:lucene-queryparser:9.4.0'
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation 'com.auth0:java-jwt:4.0.0'
    implementation 'net.lingala.zip4j:zip4j:2.11.2'
    implementation 'org.json:json:20220924'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.mockito:mockito-all:1.9.5'
}
//Jar creation
jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        if(withDependencies){
         attributes(
                'Main-Class': 'fr.techad.edc.httpd.EdcWebServer'
         )
        }
    }
from {
    if(withDependencies){
        configurations.implementation.collect { it.isDirectory() ? it : zipTree(it) }
    }
  }
}

shadowJar {
    archiveBaseName.set('edc-httpd-java')
    archiveClassifier.set('')
    archiveVersion.set('2.1.0')
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'fr.techad.edc.httpd.EdcWebServer'
    }
}

//Execute Junit tests
test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'edc-httpd'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            //Creating the pom
            pom {
                name = 'edc httpd'
                description = 'Embedded httpd server to serve the help documentation published by edc'
                url = 'https://www.easydoccontents.com'
                licenses {
                    license {
                         name = 'MIT License'
                         url = 'http://www.opensource.org/licenses/mit-license.php'
                         distribution =  'repo'
                    }
                }
                developers {
                    developer {
                    name = 'Gregory Cochin'
                    email= 'gregory.cochin@tech-advantage.com'
                    }
                    developer {
                    name = 'Mathieu Benard'
                    email= 'mathieu.benard@tech-advantage.com'
                    }
                    developer {
                    name = 'Florian Bracq'
                    email= 'florian.bracq@tech-advantage.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/tech-advantage/edc-httpd.git'
                    developerConnection = 'scm:git:ssh://github.com/tech-advantage/edc-httpd.git'
                    url = 'https://github.com/tech-advantage/edc-httpd'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}
signing {
    setRequired {
        // signing is only required if the artifacts are to be published
        gradle.taskGraph.allTasks.any { it.equals( PublishToMavenRepository) }
    }
    sign publishing.publications.mavenJava
    sign configurations.archives
}
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}
artifacts {
    shadowJar
    archives sourcesJar
    archives javadocJar
}
javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
tasks.build.dependsOn tasks.shadowJar
task install(dependsOn: publishToMavenLocal) {
    group = 'Publishing'
    description = 'Installs artifacts to local Maven repository'
}

task release() {
    group 'Project'
    description 'Build project as release.'

    dependsOn build
    dependsOn javadoc
}

build.dependsOn(shadowJar);